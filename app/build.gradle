apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

Map<String, String> versions = [
        support         : '23.3.0',
        butterknife     : '8.0.1',
        picasso         : '2.5.2',
        stetho          : '1.3.1',
        junit           : '4.12',
        retrofit        : '2.0.2'
]

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    def keysPropertiesFile = rootProject.file("keys.properties")
    def keysProperties = new Properties()
    keysProperties.load(new FileInputStream(keysPropertiesFile))

    defaultConfig {
        applicationId "com.example.android.popularmovies"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    buildTypes.each {
        it.buildConfigField 'String', 'THE_MOVIE_DB_API_KEY', keysProperties['themoviedbApiKey']
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile "junit:junit:$versions.junit"
    compile "com.android.support:appcompat-v7:$versions.support"
    compile "com.android.support:design:$versions.support"
    compile "com.squareup.picasso:picasso:$versions.picasso"
    compile "com.android.support:support-v4:$versions.support"
    compile "com.facebook.stetho:stetho:$versions.stetho"
    compile "com.jakewharton:butterknife:$versions.butterknife"
    compile "com.android.support:support-annotations:$versions.support"
    compile "com.squareup.retrofit2:retrofit:$versions.retrofit"
    compile "com.squareup.retrofit2:converter-gson:$versions.retrofit"

    apt "com.jakewharton:butterknife-compiler:$versions.butterknife"
}
